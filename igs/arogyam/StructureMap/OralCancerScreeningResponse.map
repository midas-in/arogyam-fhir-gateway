map "https://midas.iisc.ac.in/fhir/StructureMap/OralCancerScreeningResponse" = 'OralCancerScreeningResponse'

uses "http://hl7.org/fhir/StructureDefinition/Bundle" as target
uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" as source


group OralCancerScreeningResponse(source src : QuestionnaireResponse, target bundle: Bundle) {
    src -> bundle.entry as bundleClinicalImpressionEntry,
        bundle.entry as conditionEntry,
        bundle.entry as raEntry,
        bundleClinicalImpressionEntry.resource = create('ClinicalImpression') as impression,
        create('Condition') as condition,
        create('RiskAssessment') as ra then {

        src.item as provisionDiagnosisItem where(linkId = 'provisional-diagnosis') then {
            provisionDiagnosisItem.answer first as answer -> 
                impression.id = uuid(),
                condition.id = uuid(),
                impression.encounter = evaluate(src,$this.encounter),
                impression.status = 'completed',
                impression.description = 'Clinical impression by Specialist or Senior Specialist for a proabbale oral cancer patient',
                impression.subject = evaluate(src,$this.subject),
                impression.date = evaluate(src, $this.authored),
                impression.assessor = evaluate(src,$this.author),
                impression.finding as finding,
                finding.itemCodeableConcept = create('CodeableConcept') as findingCC,
                findingCC.coding = evaluate(answer,$this.value),
                condition.encounter = evaluate(src,$this.encounter),
                condition.subject = evaluate(src,$this.subject),
                condition.clinicalStatus = cc('http://terminology.hl7.org/CodeSystem/condition-clinical','active','Active'),
                condition.verificationStatus = cc('http://terminology.hl7.org/CodeSystem/condition-ver-status','confirmed','Confirmed'),
                condition.code as conditionCode,
                conditionCode.coding = evaluate(answer,$this.value),
                condition.asserter = evaluate(src,$this.author),
                impression.problem = create('Reference') as impressionProblemRef, 
                impressionProblemRef.reference = evaluate(condition,'Condition/' + $this.id),
                conditionEntry.resource = condition "rule_populate_impression";
        } "rule_prov";

        src.item as recommendationItem where(linkId = 'recommendation') then {
            recommendationItem.answer first as answer -> 
                ra.id = uuid(),
                ra.status = 'final',
                ra.subject = evaluate(src, $this.subject),
                ra.encounter = evaluate(src, $this.encounter),
                ra.performer = evaluate(src, $this.author),
                ra.prediction as prediction,
                ra.condition = create('Reference') as raConditionRef,
                raConditionRef.reference = evaluate(condition,'Condition/' + $this.id),
                prediction.outcome as predictionCC,
                predictionCC.coding = evaluate(answer,$this.value),
                impression.prognosisReference = create('Reference') as impressionPrognosisRef,
                impressionPrognosisRef.reference = evaluate(ra,'RiskAssessment/' + $this.id),
                raEntry.resource = ra "rule_populate_ra";
        } "rule_rec_ra";

        src.item as bodySiteItem where(linkId='oral-cavity-site') then {
            bodySiteItem.answer first as answer -> condition.bodySite = create('CodeableConcept') as bodyCC, bodyCC.coding = evaluate(answer,$this.value) "rule_condition_body_site";
        } "rule_body_site";
            
        src.item as noteItem where(linkId = 'note') then {
            noteItem.answer first as answer -> 
                impression.summary = evaluate(answer,$this.value) "rule_populate_impression_note";
        } "rule_impression_note";

    } "rule_bundle_entry_impression";

    src.item as recommendationItem where(linkId = 'recommendation') then {
        recommendationItem.answer as answer -> bundle.entry as taskEntry, taskEntry.resource = create('Task') as task,
            task.id = uuid(),
            task.encounter = evaluate(src,$this.encounter),
            task.for = evaluate(src,$this.subject),
            task.status = 'requested',
            task.intent = 'order',
            task.priority = 'routine',
            task.code = cc('http://hl7.org/fhir/CodeSystem/task-code','fulfill','Fulfill the focal request'),
            task.description = evaluate(answer, 'Follow up: ' + $this.value.display),
            task.authoredOn = evaluate(src ,now()),
            task.lastModified = evaluate(src ,now()) "rule_task_map";
    } "rule_followup";
}
