map "https://midas.iisc.ac.in/fhir/StructureMap/OralCancerLabelImageResponse" = 'OralCancerLabelImageResponse'

uses "http://hl7.org/fhir/StructureDefinition/Bundle" as target
uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" as source


group OralCancerLabelImageResponse(source src : QuestionnaireResponse, target bundle: Bundle) {
    src -> bundle.entry as bundleClinicalImpressionEntry, bundleClinicalImpressionEntry.resource = create('ClinicalImpression') as impression then {
        src.item as provisionDiagnosisItem where(linkId = 'provisional-diagnosis') then {
            provisionDiagnosisItem.answer first as answer -> impression.id = uuid(),
                impression.encounter = evaluate(src,$this.encounter),
                impression.status = 'completed',
                impression.description = 'Clinical impression by Reade to label the images',
                impression.subject = evaluate(src,$this.subject),
                impression.date = evaluate(src, $this.authored),
                impression.assessor = evaluate(src,$this.author),
                impression.finding as finding,
                finding.itemCodeableConcept = create('CodeableConcept') as cc,
                cc.coding = evaluate(answer,$this.value)
                then AddMedia(src, finding) "rule_populate_impression";
        } "rule_prov";
    } "rule_bundle_entry_impression";
}

group AddMedia(source src: QuestionnaireResponse, target finding){
    src.item as mediaItem where(linkId = 'media-id') then {
        mediaItem.answer first as answer -> finding.itemReference = create('Reference') as ref, ref.reference = evaluate(answer, 'Media/' + $this.value) "rule_media_rt";
    } "rule_add_media";
}
